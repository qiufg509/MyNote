# 批处理指令

1. **help**：帮助（help for查看指令for的使用方法）

2. **@**：控制不打印当前指令

3. **echo off**：之后的指令都不打印（对应echo on）

4. **echo**：打印输出(默认打印在命令提示符窗口

   ```batch
   ::将content输出到文件filename中
   echo content>filename 
   ```

5. **rem**：注释

6. **%number**：【number为1-9，%0为当前指令】传递或者引用参数

7. **shift**：参数前移1位（如传入参数aa bb cc，%1为aa，执行shift后，%1为bb）

8. **%cd%**：当前路径

9. **setlocal**：在执行 SETLOCAL 之后所做的环境改动只限于批处理文件。要还原原先的设置，必须执行 ENDLOCAL。达到批处理文件结尾时，对于该批处理文件的每个尚未执行的 SETLOCAL 命令，都会有一个隐含的 ENDLOCAL 被执行

10. **if**：（ELSE 子句必须出现在同一行上的 IF 之后且左括号"("前必须有空格）
    `IF [NOT] ERRORLEVEL number command`
    `IF [NOT] string1==string2 command`
    `IF [NOT] EXIST filename command`

    ```batch
    IF EXIST filename (
    	del filename
    ) ELSE (
    	echo filename
    )
    ```

11. **pause**：暂停执行，按任意键继续

12. **exit**：退出命令提示符窗口

13. **set**：赋值

    ```batch
    ::给变量args1赋值为xyz
    set args1=xyz
    ```

    ```batch
    ::将变量args1中的所有x替换为y然后将结果赋值给变量args2
    set args2=%args1:x=y%
    ```

14. **/p**：通过外部输入为参数赋值，搭配set使用（set /p f="apk name"）

15. **/a**：是指定一个变量等于一串运算字符（set /a f=%f%+1）

16. **timeout** /T number [/nobreak]：等待number秒，按一个键继续(/nobreak忽略按键)
    其他延时

    ```batch
    @echo off
    echo %time%
    call :delay 5000
    echo %time%
    pause
    exit
    
    :delay
    echo WScript.Sleep %1>delay.vbs
    CScript //B delay.vbs
    del delay.vbs
    goto :eof
    ```

    ```batch
    ping /n 3 127.0.0.1 >nul
    ```

    ```batch
    for /l %%i in (1,1,5000) do echo %%i>nul
    ```

17. **>null**：屏蔽执行指令显示的内容(防在指令后)

18. **type** filename：查看文本文档filename的内容详情

19. **call** filename：调用一个外部批处理脚本程序

20. **for**：循环
    `FOR /D %variable IN (set) DO command [command-parameters]`
    列出当前目录下与指定规则匹配的所有目录名
    `FOR /R [[drive:]path] %variable IN (set) DO command [command-parameters]`
    列出指定目录下雨指定规则匹配的所有文件名
    `FOR /L %variable IN (start,step,end) DO command [command-parameters]`
    从start到end以步长step遍历
    `FOR /F ["options"] %variable IN (file-set|"string"|'command') DO command 
    [command-parameters]`
    file-set一个或多个文件、string字符串、command指令、options可选(delims指定分隔符、tokens限定列范围、skip限定忽略的前几行、eol指定忽略指定规则的行)

21. **arp -a**：查询系统中缓存的ARP表(查看局域网中所有设备IP地址)

22. **FIND** [/V][/C] [/N][/I] [/OFF[LINE]] "string" [[drive:][path]filename[ ...]]

23. **TREE** [drive:][path] [/F][/A]：以树形结构列出指定目录下所有文件名

24. **dir** [C:][path][filename][/o][/s][/p][/w][/a]：列出目录下的文件和子目录

25. **fc** filename1 filename2：比价两个文件并列出差异处

26. **copy**：复制文件或文件夹

27. **move**：移动文件或文件夹

28. **md**：创建文件夹

29. **rename**：重命名文件或文件夹

30. **del**：删除文件

31. **rd**：删除文件夹

32. 字符串截取

    ```bash
    @echo off 
    set abc=hello world, this string come from bat 
    echo 原字符串为:%abc% 
    echo 截取前5个字符:%abc:~0,5% 
    echo 截取最后5个字符:%abc:~-5% 
    echo 截取第一个到倒数第6个字符:%abc:~1,-5% 
    echo 从第4个字符开始截取5个字符:%abc:~3,5% 
    echo 从倒数第14个字符开始截取5个字符:%abc:~-14,5%
    
    echo 当前时间是:%time% 即 %time:~0,2%点%time:~3,2%分%time:~6,2%秒%time:~9,2%厘秒 
    pause
    ```

33. setlocal 与 变量延迟  （变量延迟的启动语句是 setlocal enabledelayedexpansion，变量要用一对叹号“!!”括起来）

    ```
    @echo off
    set a=4
    set a=5 & echo %a%
    pause
    ::不使用延迟变量输出4（如果使用延迟变量输出5）
    ```

    ```
    @echo off
    setlocal enabledelayedexpansion
    for /l %%i in (1,1,5) do (
    set a=%%i
    echo !a!
    )
    ::使用延迟变量输入1 2 3 4 5 （不使用延迟变量执行异常）
    ```



#### mokey测试

```batch
monkey -p com.hampoo.home.setting --ignore-crashes --ignore-timeouts --kill-process-after-error --ignore-security-exceptions --throttle 200 -v -v -v -s 5 100000000 1>>/sdcard/log.log 2>>/sdcard/log_err.log
```



#### connect to PC

```batch
@echo off

echo @echo off > %cd%\doConnect.bat
echo set ip=%%1 >> %cd%\doConnect.bat
echo if "%%ip%%"=="" goto end >> %cd%\doConnect.bat
echo for /f "delims= " %%%%i in ('adb connect %%ip%%') do ( >> %cd%\doConnect.bat
echo 	if not "%%%%i"=="connected" ( >> %cd%\doConnect.bat
echo 		echo unable to connect to %%ip%% >> %cd%\doConnect.bat
echo 		goto end >> %cd%\doConnect.bat
echo 	) >> %cd%\doConnect.bat
echo ) >> %cd%\doConnect.bat
echo adb root >> %cd%\doConnect.bat
echo timeout /t 10 /nobreak >> %cd%\doConnect.bat
echo adb remount >> %cd%\doConnect.bat
echo adb shell "echo 2 > /sys/bus/platform/drivers/usb20_otg/force_usb_mode" >> %cd%\doConnect.bat
echo adb disconnect %%ip%% >> %cd%\doConnect.bat
echo :end >> %cd%\doConnect.bat

set /p ip="Device IP = "
if "%ip%"=="" (
	for /f "skip=3 delims= " %%i in ('arp -a') do (
		call %cd%\doConnect.bat %%i
	)
) else (
	call %cd%\doConnect.bat %ip%
)

pause
```



#### connect to PC（优化版）

```batch
@echo off

set /p ip="Device IP = "
if "%ip%"=="" (
	for /f "skip=3 delims= " %%i in ('arp -a') do (
		call :sub %%i
	)
) else (
	call call :sub %ip%
)

pause
exit

rem -------------------------------------------------------------------------

:sub
set ip=%1
if "%ip%"=="" goto end
for /f "delims= " %%i in ('adb connect %%ip%%') do (
	if "%%i"=="unable" (
		echo Unable to connect to Computer %ip%
		goto endSub
	)
)
adb root
timeout /t 10 >nul
adb remount  >nul
set usb_conf=/sys/bus/platform/drivers/usb20_otg/force_usb_mode
adb shell "echo 2 > %usb_conf%"  >nul
adb root
adb disconnect %ip%  >nul
timeout /t 2  >nul
adb remount  >nul

:endSub
goto :eof

```

#### 遥控器

```
@echo off
goto start
:sub
set /p keycode="KeyCode(q退出):"
if "%keycode%"=="q" goto end
if %keycode% gtr 0  (
	if %keycode% lss 260 (
		adb shell input keyevent %keycode%
		goto :eof
	)
)
echo invalid keycode.
goto :eof

:start
for /l %%i in (1,1,5) do (
	call :sub
)
goto start

:end
exit
```

#### COC降杯

```
@echo off

:start
set /a freq=%freq%+1
echo %freq%

adb shell input tap 125 950
timeout /t 1
adb shell input tap 420 810
timeout /t 3
::adb shell input tap 1175 980
adb shell input tap 250 980
adb shell input tap 1650 620
adb shell input tap 150 810
adb shell input tap 1160 700
timeout /t 1
adb shell input tap 960 960
timeout /t 2

if %freq%==100 goto end
goto start

:end
pause
```

#### 压缩apk资源——（360渠道包加固）——重签名——walle渠道包

```
@echo off
goto start

:sub
set filePath=%1
set fileName=%~n1
set channel=%fileName:~-26,10%
set toolPath=D:\GitHub\MyNote\File
::混淆资源
java -jar %toolPath%\AndResGuard\andresguard-1.1.jar %filePath% -config %toolPath%\AndResGuard\config.xml -out %USERPROFILE%\Desktop\temp\%channel% -7zip "C:\Program Files\7-Zip\7z.exe" -zipalign %ANDROID_HOME%\build-tools\28.0.3\zipalign.exe
::360包加固
set unsigned=%USERPROFILE%\Desktop\temp\%channel%\%fileName%_unsigned.apk
if /I %channel% equ BDP1000002 ( call :jiagu %unsigned% )
::重签名
java -jar %ANDROID_HOME%\build-tools\28.0.3\lib\apksigner.jar sign --ks  D:\Project\Forest\app\forest.jks --ks-key-alias forest --ks-pass pass:forest888 --key-pass pass:forest888 --out %USERPROFILE%\Desktop\temp\%fileName%.apk %unsigned%
::瓦力渠道包
java -jar %toolPath%\walle-cli-all.jar put -c %channel% %USERPROFILE%\Desktop\temp\%fileName%.apk
move %USERPROFILE%\Desktop\temp\%fileName%_%channel%.apk %USERPROFILE%\Desktop\output\%fileName%.apk
goto :eof

:jiagu
set _360Path=E:\jiagu
set un_jiagu=%1
%_360Path%\java\bin\java -jar %_360Path%\jiagu.jar -login bidepan88 BIDEPAN888
%_360Path%\java\bin\java -jar %_360Path%\jiagu.jar -config -crashlog -analyse -nocert
%_360Path%\java\bin\java -jar %_360Path%\jiagu.jar -importsign D:\Project\Forest\app\forest.jks forest888 forest forest888
%_360Path%\java\bin\java -jar %_360Path%\jiagu.jar -jiagu %un_jiagu% %~d1%~p1
del /Q %un_jiagu%
rename %un_jiagu:~0,-4%_125_jiagu.apk %~n1.apk
goto :eof

:start
md %USERPROFILE%\Desktop\output
FOR /R %USERPROFILE%\Desktop\input %%i IN (*.apk) DO (
	call :sub %%i
)

::java -jar apksigner.jar verify -v my.apk 查看签名信息
::java -jar walle-cli-all.jar show my.apk 查看渠道信息
:end
pause>nul
```

```batch
@echo off
goto start

:sub
set filePath=%1
set fileName=%~n1
set channel=%fileName:~-26,10%
set toolPath=D:\GitHub\MyNote\File
::混淆资源
java -jar %toolPath%\AndResGuard\andresguard-1.1.jar %filePath% -config %toolPath%\AndResGuard\config.xml -out %USERPROFILE%\Desktop\temp\%channel% -7zip "C:\Program Files\7-Zip\7z.exe" -zipalign %ANDROID_HOME%\build-tools\28.0.3\zipalign.exe
::360包加固
set unsigned=%USERPROFILE%\Desktop\temp\%channel%\%fileName%_unsigned.apk
call :jiagu %unsigned%
::重签名
java -jar %ANDROID_HOME%\build-tools\28.0.3\lib\apksigner.jar sign --ks  D:\Project\Forest\app\forest.jks --ks-key-alias forest --ks-pass pass:forest888 --key-pass pass:forest888 --out %USERPROFILE%\Desktop\output\%fileName%.apk %unsigned%
::瓦力渠道包
java -jar %toolPath%\walle-cli-all.jar batch -c BDP1000000,BDP1000002,BDP1000003,BDP1000004,BDP1000005,BDP1000006,BDP1000007,BDP1000008,BDP1000009,BDP1000010,BDP1000011,BDP1000012,BDP1000013,BDP1000014,BDP1000015,BDP1000016,BDP1000017,BDP1000018,BDP1000019,BDP1000020,BDP1000021 %USERPROFILE%\Desktop\output\%fileName%.apk
del %USERPROFILE%\Desktop\output\%fileName%.apk
::重命名
set now=%date:~0,4%%date:~5,2%%date:~8,2%-%time:~0,2%%time:~3,2%%time:~6,2%
echo %fileName%|findstr "__" > nul
if %errorlevel% equ 0 (
	for /f "tokens=1-9 delims=_." %%i IN ('dir %USERPROFILE%\Desktop\output /b') do ren %USERPROFILE%\Desktop\output\%%i_%%j.%%k.%%l__%%m_%%n_%%o_%%p.%%q %%i_%%j.%%k.%%l_%%p_%now: =0%_%%m.%%q
) else (
	for /f "tokens=1-9 delims=_." %%i IN ('dir %USERPROFILE%\Desktop\output /b') do ren %USERPROFILE%\Desktop\output\%%i_%%j.%%k.%%l_%%m_%%n_%%o_%%p.%%q %%i_%%j.%%k.%%l_%%p_%now: =0%_%%m.%%q
)
goto :eof

:jiagu
set _360Path=E:\jiagu
set un_jiagu=%1
%_360Path%\java\bin\java -jar %_360Path%\jiagu.jar -login bidepan88 BIDEPAN888
%_360Path%\java\bin\java -jar %_360Path%\jiagu.jar -config -crashlog -analyse -nocert
%_360Path%\java\bin\java -jar %_360Path%\jiagu.jar -importsign D:\Project\Forest\app\forest.jks forest888 forest forest888
%_360Path%\java\bin\java -jar %_360Path%\jiagu.jar -jiagu %un_jiagu% %~d1%~p1
del /Q %un_jiagu%
FOR /R %~d1%~p1 %%i IN (*_jiagu.apk) DO (
	rename %%i %~n1.apk
)
goto :eof

:start
md %USERPROFILE%\Desktop\output
FOR /R %USERPROFILE%\Desktop\input %%i IN (*.apk) DO (
	call :sub %%i
)

@echo -----------------mission completed-----------------
:showResult
set /p apkPath="Absolute Path(press q exit):"
if "%apkPath%"=="q" (
	goto end
)
java -jar %ANDROID_HOME%\build-tools\28.0.3\lib\apksigner.jar verify -v %apkPath%
java -jar %toolPath%\walle-cli-all.jar show %apkPath%
goto showResult

:end
```

#### 导出手机已安装apk

```
@echo off
adb devices
set /p deviceId="Please enter the device ID: "
set /p filter="Filter package name: "
adb -s %deviceId% shell pm list packages %filter%
set /p package="Please enter the name of the apk package to be exported: "
FOR /F "tokens=2 delims=:" %%i IN ('adb -s %deviceId% shell pm path %package%') do call :sub %%i
echo ------------------end------------------
goto end

:sub
adb -s %deviceId% pull %1  %USERPROFILE%\desktop
goto :eof

:end
pause
```

#### 非ROOT通过WIFI连接手机

```
打开USB调试模式，手机通过USB连接电脑
adb tcpip 5555
adb connect <IP Address>:5555
```

#### 文本转换成vcf通讯录

```
@echo off
echo 确保original.txt和card.vcf为ANSI格式
for /f "delims=----; tokens=1-7" %%i in ( %userprofile%\Desktop\original.txt ) do (
	call :sub %%i %%j %%k %%l %%m %%n %%o
)
set /p="转换编码为UTF-8后回车导入手机"
adb push %userprofile%\Desktop\card.vcf /sdcard
goto end

:sub
echo BEGIN:VCARD >> %userprofile%\Desktop\card.vcf
echo EMAIL;type=INTERNET;type=pref:%1>> %userprofile%\Desktop\card.vcf
echo TEL;type=CELL;type=VOICE;type=pref:%6>> %userprofile%\Desktop\card.vcf
set name=%3
echo N:%name:~0,1%;%name:~1%;;;>> %userprofile%\Desktop\card.vcf
echo FN:%name%>> %userprofile%\Desktop\card.vcf
echo VERSION:3.0>> %userprofile%\Desktop\card.vcf
echo END:VCARD>> %userprofile%\Desktop\card.vcf
goto :eof

:end
pause
```

